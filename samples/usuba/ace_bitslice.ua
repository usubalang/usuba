# Specification:
# https://uwaterloo.ca/communications-security-lab/sites/ca.communications-security-lab/files/uploads/files/ace-spec-round2.pdf

node f(x:b32) returns (y:b32)
let
    y = ((x <<< 5) & x) ^ (x <<< 1)
tel

node simeck_box(input:u32x2, rc:u32) returns (output:u32x2)
vars
    round:u32x2[9]
let
    round[0] = input;

   _no_unroll forall i in [0, 7] {
      round[i+1][0,1] = (f(round[i][0]) ^ round[i][1] ^ 0xfffffffe ^ ((rc >> i) & 1), round[i][0]) ;
    }

    output = round[8]
tel


node ACE_step(A,B,C,D,E:u32x2,RC,SC:u32[3]) returns (Ar,Br,Cr,Dr,Er:u32x2)
let
    A := simeck_box(A,RC[0]);
    C := simeck_box(C,RC[1]);
    E := simeck_box(E,RC[2]);
    B := B ^ C ^ (0,SC[0]) ^ (0xffffffff,0xffffff00);
    D := D ^ E ^ (0,SC[1]) ^ (0xffffffff,0xffffff00);
    E := E ^ A ^ (0,SC[2]) ^ (0xffffffff,0xffffff00);
    (Ar, Br, Cr, Dr, Er) = (D, C, A, E, B);
tel


node ACE(input:u32x2[5]) returns (output:u32x2[5])
vars
    SC:u32[3][16],
    RC:u32[3][16],
    tmp:u32x2[17][5]
let
    SC = (0x50,0x5c,0x91,0x8d,0x53,0x60,0x68,0xe1,0xf6,0x9d,0x40,0x4f,0xbe,0x5b,0xe9,0x7f,
          0x28,0xae,0x48,0xc6,0xa9,0x30,0x34,0x70,0x7b,0xce,0x20,0x27,0x5f,0xad,0x74,0x3f,
          0x14,0x57,0x24,0x63,0x54,0x18,0x9a,0x38,0xbd,0x67,0x10,0x13,0x2f,0xd6,0xba,0x1f);
    RC = (0x07,0x0a,0x9b,0xe0,0xd1,0x1a,0x22,0xf7,0x62,0x96,0x71,0xaa,0x2b,0xe9,0xcf,0xb7,
          0x53,0x5d,0x49,0x7f,0xbe,0x1d,0x28,0x6c,0x82,0x47,0x6b,0x88,0xdc,0x8b,0x59,0xc6,
          0x43,0xe4,0x5e,0xcc,0x32,0x4e,0x75,0x25,0xfd,0xf9,0x76,0xa0,0xb0,0x09,0x1e,0xad);

    tmp[0] = input;

    forall i in [0, 15] {
        tmp[i+1] = ACE_step(tmp[i], RC[0,1,2][i],SC[0,1,2][i]);
    }

    output = tmp[16];
tel