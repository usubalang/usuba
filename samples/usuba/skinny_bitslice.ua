node sbox_calc(a:b8) returns (b:b8)
let
    b = (a[0], a[1], a[2], a[3] ^ ~(a[0] | a[1]),
         a[4], a[5], a[6], a[7] ^ ~(a[4] | a[5]))
tel

perm sbox_perm(a:b8) returns (b:b8) {
     6, 7, 1, 2, 4, 8, 5, 3
}

perm sbox_last_perm(a:b8) returns (b:b8) {
     1, 2, 3, 4, 5, 7, 6, 8
}

node sbox_quarter(a:b8) returns (b:b8)
let
    b = sbox_perm(sbox_calc(a))
tel

node sbox_last_quarter(a:b8) returns (b:b8)
let
    b = sbox_last_perm(sbox_calc(a))
tel

node sbox(a:b8) returns (b:b8)
let
    b = sbox_last_quarter(sbox_quarter(sbox_quarter(sbox_quarter(a))))
tel

# table sbox(a:b8) returns (b:b8) {
#    0x65,0x4c,0x6a,0x42,0x4b,0x63,0x43,0x6b,0x55,0x75,0x5a,0x7a,0x53,0x73,0x5b,0x7b,
#    0x35,0x8c,0x3a,0x81,0x89,0x33,0x80,0x3b,0x95,0x25,0x98,0x2a,0x90,0x23,0x99,0x2b,
#    0xe5,0xcc,0xe8,0xc1,0xc9,0xe0,0xc0,0xe9,0xd5,0xf5,0xd8,0xf8,0xd0,0xf0,0xd9,0xf9,
#    0xa5,0x1c,0xa8,0x12,0x1b,0xa0,0x13,0xa9,0x05,0xb5,0x0a,0xb8,0x03,0xb0,0x0b,0xb9,
#    0x32,0x88,0x3c,0x85,0x8d,0x34,0x84,0x3d,0x91,0x22,0x9c,0x2c,0x94,0x24,0x9d,0x2d,
#    0x62,0x4a,0x6c,0x45,0x4d,0x64,0x44,0x6d,0x52,0x72,0x5c,0x7c,0x54,0x74,0x5d,0x7d,
#    0xa1,0x1a,0xac,0x15,0x1d,0xa4,0x14,0xad,0x02,0xb1,0x0c,0xbc,0x04,0xb4,0x0d,0xbd,
#    0xe1,0xc8,0xec,0xc5,0xcd,0xe4,0xc4,0xed,0xd1,0xf1,0xdc,0xfc,0xd4,0xf4,0xdd,0xfd,
#    0x36,0x8e,0x38,0x82,0x8b,0x30,0x83,0x39,0x96,0x26,0x9a,0x28,0x93,0x20,0x9b,0x29,
#    0x66,0x4e,0x68,0x41,0x49,0x60,0x40,0x69,0x56,0x76,0x58,0x78,0x50,0x70,0x59,0x79,
#    0xa6,0x1e,0xaa,0x11,0x19,0xa3,0x10,0xab,0x06,0xb6,0x08,0xba,0x00,0xb3,0x09,0xbb,
#    0xe6,0xce,0xea,0xc2,0xcb,0xe3,0xc3,0xeb,0xd6,0xf6,0xda,0xfa,0xd3,0xf3,0xdb,0xfb,
#    0x31,0x8a,0x3e,0x86,0x8f,0x37,0x87,0x3f,0x92,0x21,0x9e,0x2e,0x97,0x27,0x9f,0x2f,
#    0x61,0x48,0x6e,0x46,0x4f,0x67,0x47,0x6f,0x51,0x71,0x5e,0x7e,0x57,0x77,0x5f,0x7f,
#    0xa2,0x18,0xae,0x16,0x1f,0xa7,0x17,0xaf,0x01,0xb2,0x0e,0xbe,0x07,0xb7,0x0f,0xbf,
#    0xe2,0xca,0xee,0xc6,0xcf,0xe7,0xc7,0xef,0xd2,0xf2,0xde,0xfe,0xd7,0xf7,0xdf,0xff }

node SubCell(state:b8[4][4]) returns (stateR:b8[4][4])
let
    forall i in [0, 3] {
      forall j in [0, 3] {
        stateR[i][j] = sbox(state[i][j])
      }
    }
tel

node AddConstants(state:b8[4][4], c:b8) returns (stateR:b8[4][4])
let
    stateR[0][0] = state[0][0] ^ (c & 0xf:b8);
    stateR[1][0] = state[1][0] ^ ((c >> 4) & 0x3:b8);
    stateR[2][0] = state[2][0] ^ 0x2:b8;

    stateR[0,1,2][1,2,3] = state[0,1,2][1,2,3];
    stateR[3] = state[3];
tel

node AddKey(state:b8[4][4], keyCells:b8[2][4][4])
     returns (stateR:b8[4][4], keyCellsR:b8[2][4][4])
vars
    keyCells16 : b8[2][16],
    keyCells_tmp : b8[2][4][4]
let
    // apply the subtweakey to the internal state
    forall i in [0, 1] {
        forall j in [0, 3] {
            stateR[i][j] = state[i][j] ^ keyCells[0][i][j] ^ keyCells[1][i][j];
        }
    }
    stateR[2,3] = state[2,3];

    // update the subtweakey states with the permutation
    keyCells16 = keyCells;
    keyCells16[0] := keyCells16[0][9,15,8,13,10,14,12,11,0,1,2,3,4,5,6,7];
    keyCells16[1] := keyCells16[1][9,15,8,13,10,14,12,11,0,1,2,3,4,5,6,7];
    keyCells_tmp = keyCells16;

    // update the subtweakey states with the LFSRs
     forall i in [0, 1] {
       forall j in [0, 3] {
        keyCellsR[1][i][j] = ((keyCells_tmp[1][i][j]<<1)&0xfe:b8)^((keyCells_tmp[1][i][j]>>7)&0x01:b8)^((keyCells_tmp[1][i][j]>>5)&0x01:b8);
      }
    }
    keyCellsR[0] = keyCells_tmp[0];
    keyCellsR[1][2,3] = keyCells_tmp[1][2,3]
tel

node ShiftRows(state:b8[16]) returns (stateR:b8[16])
let
    stateR = state[0, 1, 2, 3, 7, 4, 5, 6, 10, 11, 8, 9, 13, 14, 15, 12]
tel

node MixColumn(state:b8[4][4]) returns (stateR:b8[4][4])
let
    forall j in [0, 3] {
        state[1][j] ^:= state[2][j];
        state[2][j] ^:= state[0][j];
        state[3][j] ^:= state[2][j];

        stateR[3,2,1,0][j] = state[2,1,0,3][j];
    }
tel

node Skinny (input:b8[4][4], tweaky:b8[32]) returns (output:b8[4][4])
vars
    state: b8[49][4][4],
    keyCells : b8[49][2][4][4],
    RC : b8[48],
    tmp : b8[48][4][4]
let
    RC = ( 0x01:b8, 0x03:b8, 0x07:b8, 0x0F:b8, 0x1F:b8, 0x3E:b8, 0x3D:b8, 0x3B:b8, 0x37:b8, 0x2F:b8,
		   0x1E:b8, 0x3C:b8, 0x39:b8, 0x33:b8, 0x27:b8, 0x0E:b8, 0x1D:b8, 0x3A:b8, 0x35:b8, 0x2B:b8,
		   0x16:b8, 0x2C:b8, 0x18:b8, 0x30:b8, 0x21:b8, 0x02:b8, 0x05:b8, 0x0B:b8, 0x17:b8, 0x2E:b8,
		   0x1C:b8, 0x38:b8, 0x31:b8, 0x23:b8, 0x06:b8, 0x0D:b8, 0x1B:b8, 0x36:b8, 0x2D:b8, 0x1A:b8,
		   0x34:b8, 0x29:b8, 0x12:b8, 0x24:b8, 0x08:b8, 0x11:b8, 0x22:b8, 0x04:b8 );

    state[0] = input;
    keyCells[0] = tweaky;

    forall i in [0, 47] {
        (tmp[i], keyCells[i+1]) = AddKey(AddConstants(SubCell(state[i]), RC[i]), keyCells[i]);
        state[i+1] = MixColumn(ShiftRows(tmp[i]))
    }

    output = state[48]

tel